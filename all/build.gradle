plugins {
    id "java"
}

description = "OpenTelemetry All"
ext.moduleName = "io.opentelemetry.all"

def subprojects = [
        project(':opentelemetry-api'),
        project(':opentelemetry-context'),
        project(':opentelemetry-context-prop'),
        project(':opentelemetry-extension-runtime-metrics'),
        project(':opentelemetry-extension-trace-propagators'),
        project(':opentelemetry-extension-trace-utils'),
        project(':opentelemetry-extension-auto-annotations'),
        project(':opentelemetry-exporters-logging'),
        project(':opentelemetry-exporters-inmemory'),
        project(':opentelemetry-exporters-jaeger'),
        project(':opentelemetry-exporters-otlp'),
        project(':opentelemetry-exporters-prometheus'),
        project(':opentelemetry-exporters-zipkin'),
        project(':opentelemetry-integration-tests'),
        project(':opentelemetry-integration-tests-tracecontext'),
        project(':opentelemetry-opentracing-shim'),
        project(':opentelemetry-sdk-baggage'),
        project(':opentelemetry-sdk-common'),
        project(':opentelemetry-sdk-metrics'),
        project(':opentelemetry-sdk-tracing'),
        project(':opentelemetry-sdk'),
        project(':opentelemetry-sdk-extension-async-processor'),
        project(':opentelemetry-sdk-extension-otproto'),
        project(':opentelemetry-sdk-extension-testbed'),
        project(':opentelemetry-sdk-extension-jaeger-remote-sampler'),
]

for (subproject in rootProject.subprojects) {
    if (subproject == project) {
        continue
    }
    evaluationDependsOn(subproject.path)
}

dependencies {
    compile subprojects
    testCompile libraries.archunit
}

task jacocoMerge(type: JacocoMerge) {
    dependsOn(subprojects.jacocoTestReport.dependsOn)
    mustRunAfter(subprojects.jacocoTestReport.mustRunAfter)
    destinationFile = file("${buildDir}/jacoco/test.exec")
    executionData = files(subprojects.jacocoTestReport.executionData)
            .filter { f -> f.exists() }
}

jacocoTestReport {
    dependsOn(jacocoMerge)
    reports {
        xml.enabled = true
        html.enabled = true
    }

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    classDirectories.from = files(classDirectories.files.collect {
        fileTree(dir: it, excludes: [
                // Ignore generated code
                "io/opentelemetry/exporters/jaeger/proto/**"
        ])
    })
}
